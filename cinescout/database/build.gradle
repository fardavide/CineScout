apply plugin: 'cinescout.kmp.android'
apply plugin: 'app.cash.sqldelight'

cinescoutAndroid {
    namespace 'cinescout.database'
}

sqldelight {
    databases {
        Database {
            dialect(libs.sqlDelight.sqlite.dialect)
            packageName = "cinescout.database"
            schemaOutputDirectory = file("src/commonMain/sqldelight/cinescout/database/schemas")
            verifyMigrations = true
        }
    }
}

moduleDependencies {
    implementation 'utils:kotlin'
}

kotlin {
    sourceSets {
        "jvmMain" {
            dependencies {
                implementation libs.sqlDelight.sqlite
            }
        }
        "androidMain" {
            dependencies {
                implementation libs.sqlDelight.android
            }
        }
    }
}

dependencies {
    kspJvm libs.koin.ksp
    kspCommonMainMetadata libs.koin.ksp

    commonMainImplementation libs.bundles.base
    commonMainImplementation libs.sqlDelight.coroutines
    commonMainImplementation libs.sqlDelight.sqlite

    commonTestImplementation libs.bundles.test.kotlin.junit4
}

tasks.withType(Test).configureEach {
    useJUnit()
}

afterEvaluate {
    // Disable due: Test run failed to complete. Instrumentation run failed due to Process crashed.
    tasks { "connectedDebugAndroidTest" { enabled = false } }
}
